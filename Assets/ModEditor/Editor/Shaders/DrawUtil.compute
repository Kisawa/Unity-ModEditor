#pragma kernel Init
#pragma kernel InitTex
#pragma kernel Merge
#pragma kernel RefreshView
#pragma kernel Clone
#pragma kernel Write
#pragma kernel Draw

float2 _TexelSize;
RWTexture2D<float4> RW_BackgroundTexture;
RWTexture2D<float4> RW_ForegroundTexture;
RWTexture2D<float4> RW_Texture;
int _ViewType;
RWTexture2D<float4> RW_ViewTexture;

float pow2(float res)
{
	return res * res;
}

float2 rotate2(float2 res, float radian)
{
	return mul(float2x2(cos(radian), -sin(radian), sin(radian), cos(radian)), res);
}

float remap(float num, float inMin, float inMax, float outMin, float outMax)
{
	float step = inMax - inMin;
	if(step == 0)
		return outMin;
	else
		return outMin + (num - inMin) * (outMax - outMin) / (inMax - inMin);
}

float4 blend(float4 src, float4 dst, float srcF, float dstF)
{
	float4 col = dst * dstF + src * srcF;
	return col;
}

void refreshView(uint3 id, float4 col)
{
	switch(_ViewType)
	{
		case 0:
			RW_ViewTexture[id.xy] = float4(col.r, 0, 0, 1);
			break;
		case 1:
			RW_ViewTexture[id.xy] = float4(0, col.g, 0, 1);
			break;
		case 2:
			RW_ViewTexture[id.xy] = float4(0, 0, col.b, 1);
			break;
		case 3:
			RW_ViewTexture[id.xy] = float4(col.a, col.a, col.a, 1);
			break;
	}
}

void merge(uint3 id)
{
	float4 background = RW_BackgroundTexture[id.xy];
	float4 foreground = RW_ForegroundTexture[id.xy];
	float4 col = blend(foreground, background, foreground.a, 1 - foreground.a);
    RW_Texture[id.xy] = col;
	refreshView(id, col);
}

float4 _BaseColor;
[numthreads(32,32,1)]
void Init(uint3 id : SV_DispatchThreadID)
{
	RW_BackgroundTexture[id.xy] = _BaseColor;
	merge(id);
}

RWTexture2D<float4> _BaseTexture;
[numthreads(32,32,1)]
void InitTex(uint3 id : SV_DispatchThreadID)
{
	RW_BackgroundTexture[id.xy] = _BaseTexture[id.xy];
	merge(id);
}

[numthreads(32,32,1)]
void Merge(uint3 id : SV_DispatchThreadID)
{
	merge(id);
}

[numthreads(32,32,1)]
void RefreshView(uint3 id : SV_DispatchThreadID)
{
	refreshView(id, RW_Texture[id.xy]);
}

[numthreads(32,32,1)]
void Clone(uint3 id : SV_DispatchThreadID)
{
	RW_Texture[id.xy] = RW_ForegroundTexture[id.xy];
}

float4 _Color;
[numthreads(32,32,1)]
void Write(uint3 id : SV_DispatchThreadID)
{
	RW_Texture[id.xy] = _Color;
}

float2 _CursorTexcoord;
float3 _TexBrushRange;
float _BrushRotate;
int GetRange(uint3 id, out float res)
{
	float2 dir = id.xy - _CursorTexcoord * _TexelSize;
	dir = rotate2(dir, _BrushRotate);
	res = pow2(dir.x) / pow2(_TexBrushRange.x * _TexelSize.x) + pow2(dir.y) / pow2(_TexBrushRange.y * _TexelSize.y);
	int inRange = 1 - step(1, res);
	res = saturate(1 - remap(res, _TexBrushRange.z, 1, 0, 1)) * inRange;
	return inRange;
}

float4 _BrushColor;
float4 _ColorMask;
[numthreads(32,32,1)]
void Draw(uint3 id : SV_DispatchThreadID)
{
	float res;
	int inRange = GetRange(id, res);
	float4 foreground = RW_ForegroundTexture[id.xy];
	float4 col = _BrushColor;
	col = blend(col, foreground, res, 1 - res);
	col = lerp(foreground, col, _ColorMask);
	RW_ForegroundTexture[id.xy] = col;
	merge(id);
}